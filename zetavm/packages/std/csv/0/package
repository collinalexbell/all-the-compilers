#language "lang/plush/0"

var parsing = import "std/parsing/0";
var string = import "std/string/0";
var io = import "core/io/0";

var parseCell = function (input)
{
    // Eat preceding whitespace
    input:eatWS();

    // If we find a quotation mark
    if (input:next('"'))
    {
        var str = input:parseStringLit('"');
        input:eatWS();
        return str;
    }

    var str = '';

    for (;;)
    {
        var ch = input:peekCh();

        if (ch == '\r')
            continue;

        if (ch == ',')
            break;

        if (ch == '\n')
            break;

        input:readCh();
        str += ch;
    }

    // Trim trailing whitespace
    str = string.rtrim(str);

    return str;
};

var parseRow = function (input)
{
    var row = [];

    var curLineNo = input.lineNo;

    for (;;)
    {
        input:eatWS();

        if (input:eof())
            break;

        row:push(parseCell(input));

        if (input:match('\n'))
            break;

        input:expect(',');
    }

    return row;
};

/**
Read a CSV file (comma separated values)
*/
var parseFile = function (fileName)
{
    var input = parsing.fileInput(fileName);

    var rows = [];

    for (; !input:eof();)
        rows:push(parseRow(input));

    return rows;
};

/**
Write CSV data to a file
*/
var writeFile = function (fileName, rows)
{
    var strs = [];

    for (var j = 0; j < rows.length; j += 1)
    {
        var row = rows[j];

        for (var i = 0; i < row.length; i += 1)
        {
            var cell = row[i];

            // FIXME: need to escape cell strings
            strs:push('"' + cell + '"');

            if (i < row.length - 1)
                strs:push(', ');
        }

        if (j < rows.length - 1)
            strs:push('\n');
    }

    var output = string.join(strs, '');

    io.write_file(fileName, output);
};

exports.parseFile = parseFile;
exports.writeFile = writeFile;
